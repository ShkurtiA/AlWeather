apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

android {
    signingConfigs {
        config {
            keyAlias 'alweather'
            keyPassword 'a3a3liburnia'
            storeFile file('C:/Users/User/AlWeather/extras/keystore/alweather.jks')
            storePassword 'a3a3liburnia'
        }
    }
    compileSdkVersion 22
    buildToolsVersion "22.0.0"
    defaultConfig {
        applicationId "al.shkurti.weather.android"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    // When using "robototextview" library we can remove the fonts that we don't use following this lines
    android.applicationVariants.all { variant ->
        variant.mergeAssets.doFirst {
            File fonts = file("${projectDir}/build/intermediates/exploded-aar/com.github.johnkil.android-robototextview/robototextview/2.3.0/assets/fonts")
            if (fonts.exists()) {
                for (File file : fonts.listFiles()) {
                    if (file.getName().contains("RobotoSlab") || file.getName().contains("RobotoCondensed")) {
                        println("delete " + file.getName() + " font")
                        file.delete()
                    };
                }
            }
        }

        if(variant.buildType.name.equals("release")){
            variant.outputs.each
                    {
                        output ->
                            def outputFile = output.outputFile
                            def date = new Date()
                            if(outputFile!=null && outputFile.name.endsWith('.apk'))
                            {
                                def fileName = outputFile.name.replace(
                                        "mobile",
                                        "alweather-" + defaultConfig.versionName + "-" + defaultConfig.versionCode + "-" + date.format('yyyyMMdd'))
                                output.outputFile = new File((String) outputFile.parent, (String) fileName)
                            }
                    }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // com.android.support & google stuff
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    compile 'com.google.android.gms:play-services-location:7.0.0'// Google Location, Activity Recognition, and Places
    // communication
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    // view
    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        // https://github.com/keyboardsurfer/Crouton the boss says:
        // exclusion is not necessary, but generally a good idea.
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile('com.mikepenz.materialdrawer:library:2.6.9@aar') {
        transitive = true
    }
    compile 'com.github.johnkil.android-robototextview:robototextview:2.3.0'
    compile 'com.afollestad:material-dialogs:0.6.7.0'
}


